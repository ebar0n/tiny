package compilador;

import java_cup.runtime.*;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import ast.*;	

action code {:
	private boolean modoDepuracion=false;
	private NodoBase ast=null;
	private int lineanum=0;
	//Debe ser llamada solo despues de un parse(), en caso contrario retornara null siempre;
	public NodoBase getASTroot(){
		return ast;
	}

/*	public void printModoDepuracion(int lvl, String message){
		if(modoDepuracion) {
			while(lvl > 0) {
				System.out.print("");
				lvl--;
			}
			System.out.println(message); 	
	} */

	public void printModoDepuracion(String message){
		if(modoDepuracion)
			System.out.println("\t" + message); 	
	}
:}

parser code {: 
	
	public int error_count = 1;
	public void syntax_error(Symbol s) {
		ComplexSymbol cs = (ComplexSymbol) s;
		System.out.println("#Error (Sintactico)-> Linea " + (cs.xleft.getLine()) + ", Columna " + (cs.xleft.getColumn()) + " -> " + cs.getName());
		this.error_count++;	
	}

	public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception { 
	}
:}
/* Simbolos terminales */

terminal IF;
terminal THEN;
terminal ELSE;
terminal END;
terminal REPEAT;
terminal UNTIL;
terminal READ;
terminal WRITE;
terminal String ID;
terminal Integer NUM;
terminal ASSIGN;
terminal EQ;
terminal LT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal OVER;
terminal LPAREN;
terminal RPAREN;
terminal SEMI;
terminal FOR;
terminal BOOLEAN;
terminal INT;
terminal BEGIN;
terminal RETURN;
terminal VOID;
terminal NE;
terminal GE;
terminal LE;
terminal COMA;
terminal GT;
terminal LCLASP;
terminal RCLASP;
terminal AND;
terminal OR;
terminal TRUE;
terminal FALSE;

/* Simbolos No terminales */

non terminal program;
non terminal NodoBase unit;
non terminal NodoBase external_declaration;
non terminal NodoBase declarator_function;
non terminal NodoBase sentence_list;
non terminal NodoBase sentence;
non terminal NodoBase if_sentence;
non terminal NodoBase repeat_sentence;
non terminal NodoBase assign_sentence;
non terminal NodoBase read_sentence;
non terminal NodoBase write_sentence;
non terminal NodoBase expression;
non terminal NodoBase additive_expression;
non terminal NodoBase multiplicative_expression;
non terminal NodoBase factor;
non terminal NodoBase array_var_access;

non terminal NodoBase declare_variable;
non terminal NodoBase variable_list;
non terminal NodoBase for_sentence;
non terminal NodoBase return_sentence;
non terminal NodoBase declare_function;
non terminal NodoBase block_sentence;
non terminal NodoBase block_sentence_init;
non terminal NodoBase arguments_list_function;
non terminal NodoBase arguments_function;
non terminal NodoBase function_sentence;
non terminal NodoBase logical_expression;
non terminal tipoDato type_specifier;	
non terminal NodoIdentificador init_id;

/* Precedencia de operadores */

precedence left AND, OR;
precedence left GE;
precedence left GT;
precedence left LE;
precedence left LT;
precedence left PLUS, MINUS;
precedence left TIMES, OVER;
precedence left LPAREN, RPAREN;

start with program;
/* REGLAS GRAMATICALES */
program		::= unit;

unit 		::= 
			external_declaration:ed block_sentence:d SEMI
				{: 
					printModoDepuracion("program");
					NodoBase hijoActual = ed;
					if(hijoActual != null){
						while(hijoActual.TieneHermano()){
							hijoActual = hijoActual.getHermanoDerecha();
						}
						hijoActual.setHermanoDerecha(d);
						ast=ed;
					}else{
						ast=d;
					}
				:}
			| block_sentence:d SEMI
				{: 
					printModoDepuracion("program");
					ast = d;
				:}
			;

external_declaration ::=
			external_declaration:exd declare_function:fd SEMI
				{: 
					printModoDepuracion("external_declaration -> declare_function SEMI");
					NodoBase hijoActual = exd;
					if(hijoActual !=null){
						while(hijoActual.TieneHermano()){
							hijoActual = hijoActual.getHermanoDerecha();
						}
						hijoActual.setHermanoDerecha(fd);
						RESULT=exd;
					}else{
						RESULT=fd;
					}
				:}
			| declare_function:fd SEMI
				{: 
	    			printModoDepuracion("external_declaration -> declare_function");
					RESULT=fd;				
				:}
			;

declare_variable	::= 
		type_specifier:type variable_list:v
			{: 
				printModoDepuracion("declare_variable -> type_specifier variable_list");
				RESULT = new NodoVariable(v, type);
			:}
		;

init_id 		::=
		ID:variable
			{:
				RESULT = new NodoIdentificador(variable);
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());	
			:}
		;

variable_list	::= 
		init_id:variable COMA variable_list:v
			{: 
				printModoDepuracion("variable_list -> ID COMA variable_list"); 
				RESULT = new NodoVariable(variable, v);
				RESULT.setSymbol(variable.getSymbol());	
			:}
		| init_id:variable
			{: 
				printModoDepuracion("variable_list -> ID"); 
				RESULT = new NodoVariable(variable);
				RESULT.setSymbol(variable.getSymbol());	
			:}
		| init_id:variable LCLASP NUM:tam RCLASP COMA variable_list:v
			{:
				printModoDepuracion("variable_list -> ID LCLASP NUM RCLASP COMA variable_list"); 
				if(variable!=null && tam!=null){
					RESULT= new NodoArray(variable, tam, v);
					RESULT.setSymbol(variable.getSymbol());		
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla variable_list -> ID LCLASP NUM RCLASP COMA variable_list");
					System.exit(1);
				}
			:}
		| init_id:variable LCLASP NUM:tam RCLASP
			{:
				printModoDepuracion("variable_list -> ID LCLASP NUM RCLASP"); 
				if(variable!=null && tam!=null){
					RESULT= new NodoArray(variable, tam);
					RESULT.setSymbol(variable.getSymbol());	
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla variable_list -> ID LCLASP NUM RCLASP");
					System.exit(1);
				}
			:}
		;
	
declare_function ::= 
		type_specifier:type init_id:variable declarator_function:decl block_sentence:expression 
			{:
				printModoDepuracion("declare_function -> type_specifier ID declarator_function block_sentence");
				RESULT = new NodoFunction(type,variable,decl,expression);
				RESULT.setSymbol(variable.getSymbol());
			:}
		|	VOID:type init_id:variable declarator_function:decl block_sentence:expression 
			{:
				printModoDepuracion("declare_function -> VOID declarator_function block_sentence");
				RESULT = new NodoFunction(tipoDato.VOID,variable,decl,expression);
				RESULT.setSymbol(variable.getSymbol());
			:}
		;

type_specifier	::=	
		INT
			{: 
				printModoDepuracion("type_specifier -> INT ");
				RESULT = tipoDato.INT;
			:}
		|	BOOLEAN
			{: 
				printModoDepuracion("type_specifier -> BOOLEAN");
				RESULT = tipoDato.BOOLEAN;
			:}		
		;

declarator_function ::=
	| LPAREN  RPAREN
	| LPAREN arguments_list_function:ptl RPAREN
		{:
			printModoDepuracion("declarator_function -> LPAREN arguments_list_function RPAREN");
			RESULT = ptl;	
		:}
	;

block_sentence_init  ::= 
		BEGIN:init
			{:
				printModoDepuracion("block_sentence -> BEGIN init");
				RESULT = new NodoBase();
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());	
			:} 
		;

block_sentence  ::= 
		block_sentence_init:init sentence_list:expression END 
			{:
				printModoDepuracion("block_sentence -> block_sentence_init sentence_list END");
				RESULT = new NodoBloque(expression);
				RESULT.setSymbol(init.getSymbol());
			:} 
		;

arguments_list_function ::= 
		type_specifier:type init_id:valor COMA arguments_list_function:arg
			{:
				printModoDepuracion("arguments_list_function -> type_specifier ID COMA arguments_list_function");
				RESULT = new NodoArgList(valor,type,arg);
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());	
			:}
		| type_specifier:type init_id:valor
			{:
				printModoDepuracion("arguments_list_function -> type_specifier ID");
				RESULT = new NodoArgList(valor,type);
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());	
			:}
		;

sentence_list    ::=
		sentence_list:ini sentence:sent SEMI 
			{: 
				printModoDepuracion("sentence_list -> sentence_list sentence SEMI"); 
				NodoBase hijoActual = ini;
				if(hijoActual !=null){
					while(hijoActual.TieneHermano()){
						hijoActual = hijoActual.getHermanoDerecha();
					}
					hijoActual.setHermanoDerecha(sent);
					RESULT=ini;
						
				}else{
					RESULT=sent;
				}
			:}
	    | sentence:sent	SEMI
	    	{: 
	    		printModoDepuracion("sentence_list -> sentence SEMI");
				RESULT=sent;			
			:}
		;

sentence ::=
		if_sentence:is
			{:
				printModoDepuracion("sentence -> if_sentence"); 
				RESULT=is;
			:}
		| declare_variable:dv
			{: 
				printModoDepuracion("sentence -> declare_variable"); 
				RESULT=dv;
			:}	
		| declare_function:df
			{: 
				printModoDepuracion("sentence -> declare_function"); 
				RESULT=df;
			:}
		| block_sentence:bs
			{: 
				printModoDepuracion("sentence -> block_sentence"); 
				RESULT=bs;
			:}
        | repeat_sentence:rs
        	{:
        		printModoDepuracion("sentence -> repeat_sentence"); 
				RESULT=rs;
			:}
		| assign_sentence:as
			{: 
				printModoDepuracion("sentence -> assign_sentence"); 
				RESULT=as;
			:}
        | read_sentence:rs
        	{: 
        		printModoDepuracion("sentence -> read_sentence");  
				RESULT=rs;
			:}
        | write_sentence:ws
        	{: 
        		printModoDepuracion("sentence -> write_sentence");
				RESULT=ws;
			:}
		| for_sentence:fs 
			{:
				printModoDepuracion("sentence -> for_sentence"); 
				RESULT=fs;
			:}
		| return_sentence:rs
			{: 
				printModoDepuracion("sentence -> return_sentence"); 
				RESULT=rs;
			:} 
		| function_sentence:fs
			{: 
				printModoDepuracion("sentence -> function_sentence"); 
				RESULT=fs;
			:}
        | error
        	{: 
        		RESULT = null;
        		System.out.println("Ocurrio error en cup regla sentence");
        		System.exit(1);
			:}
    	;

if_sentence ::= 
	IF logical_expression:le THEN sentence_list:sl END 
		{: 
			printModoDepuracion("if_sentence -> IF logical_expression THEN sentence_list END"); 
			RESULT = new NodoIf(le,sl);
		:}
	| IF logical_expression:le THEN sentence_list:slthen ELSE sentence_list:slelse END
		{: 
			printModoDepuracion("if_sentence -> IF logical_expression THEN sentence_list ELSE sentence_list END"); 
			RESULT = new NodoIf(le,slthen,slelse);
		:}
	;

repeat_sentence ::= 
		REPEAT sentence_list:sl UNTIL logical_expression:ex
			{: 
				printModoDepuracion("repeat_sentence -> REPEAT sentence_list UNTIL expression");
				RESULT = new NodoRepeat(sl,ex);
			:}
		;

assign_sentence ::= 
		init_id:variable ASSIGN logical_expression:ex	
			{: 
				printModoDepuracion("assign_sentence -> ID ASSIGN");
				if(variable!=null){
					RESULT = new NodoAsignacion(variable,ex);
					RESULT.setSymbol(variable.getSymbol());
				}
				else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla assign_sentence -> ID");
					System.exit(1);
				}
			:}
		| array_var_access:vesc	ASSIGN logical_expression:ex
			{: 
				printModoDepuracion("assign_sentence -> array_var_access");
				if(vesc!=null && ex!=null){
					RESULT = new NodoAsignacion((NodoArray)vesc,ex);
					RESULT.setSymbol( vesc.getSymbol() );
				}
				else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla assign_sentence -> array_var_access");
					System.exit(1);
				}
			:}
		;

read_sentence   ::=
		READ init_id:variable
			{: 
				printModoDepuracion("read_sentence -> ID"); 
				if(variable!=null){
					RESULT = new NodoLeer(variable);
					RESULT.setSymbol( variable.getSymbol() );
				}
				else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla read_sentence -> ID");
					System.exit(1);
				}
			:}
		| READ array_var_access:variable
			{: 
				printModoDepuracion("read_sentence -> array_var_access"); 
				if(variable!=null){
					RESULT = new NodoLeer(variable);
					RESULT.setSymbol( variable.getSymbol() );
				}
				else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla read_sentence -> array_var_access");
					System.exit(1);
				}
			:}
		;

write_sentence  ::=
		WRITE logical_expression:ex
			{: 
				printModoDepuracion("write_sentence -> expression"); 
				RESULT= new NodoEscribir(ex);
			:}
		;

for_sentence 	::=
		FOR LPAREN assign_sentence:ini SEMI logical_expression:cond SEMI assign_sentence:inc RPAREN sentence_list:sent END 
			{: 
				printModoDepuracion("for_sentence");
				RESULT = new NodoFor(ini,cond,inc,sent);
			:}
		;

return_sentence ::= 
		RETURN logical_expression:fact
			{: 
				printModoDepuracion("return_sentence -> logical_expression");
				RESULT = new NodoReturn(fact);
			:}
		| RETURN 
			{: 
				printModoDepuracion("return_sentence -> empty");
				RESULT = new NodoReturn();
			:};

function_sentence ::= 
		init_id:variable LPAREN arguments_function:variables RPAREN 
			{:
				printModoDepuracion("function_sentence -> arguments_function");
				RESULT = new NodoCallFunction(variable,variables);
				RESULT.setSymbol(variable.getSymbol());
			:}
		| init_id:variable LPAREN RPAREN
			{:
				printModoDepuracion("function_sentence -> not arguments_function");
				RESULT = new NodoCallFunction(variable);
				RESULT.setSymbol(variable.getSymbol());
			:}
		;

logical_expression ::= 
		expression:exI AND logical_expression:exD
			{: 
				printModoDepuracion("logical_expression -> AND logical_expression");
				RESULT = new NodoLogico(exI,tipoOp.and,exD);	
				RESULT.setSymbol(exI.getSymbol());	
			:}

		| expression:exI OR logical_expression:exD
			{:
				printModoDepuracion("logical_expression -> OR logical_expression");
				RESULT = new NodoLogico(exI,tipoOp.or,exD);	
				RESULT.setSymbol(exI.getSymbol());
			:}
		| expression:exl
			{: 
				printModoDepuracion("logical_expression -> expression");
				RESULT = new NodoLogico(exl);	
				RESULT.setSymbol(exl.getSymbol());			
			:}
		;

expression  ::=
		additive_expression:exI GE additive_expression:exD
			{: 
				printModoDepuracion("expression -> GE");
				RESULT = new NodoOperacion(exI,tipoOp.mayori,exD);
				RESULT.setSymbol(exI.getSymbol());
			:} 
		| additive_expression:exI GT additive_expression:exD
			{: 
				printModoDepuracion("expression -> GT");
				RESULT = new NodoOperacion(exI,tipoOp.mayor,exD);
				RESULT.setSymbol(exI.getSymbol());
			:}
		 
		| additive_expression:exI LE additive_expression:exD
			{: 
				printModoDepuracion("expression -> LE");
				RESULT = new NodoOperacion(exI,tipoOp.menori,exD);
				RESULT.setSymbol(exI.getSymbol());
			:}
		| additive_expression:exI LT additive_expression:exD	
			{: 
				printModoDepuracion("expression -> LT");
				RESULT = new NodoOperacion(exI,tipoOp.menor,exD);
				RESULT.setSymbol(exI.getSymbol());
			:}
		| additive_expression:exI EQ additive_expression:exD
			{: 
				printModoDepuracion("expression -> EQ");
				RESULT = new NodoOperacion(exI,tipoOp.igual,exD);
				RESULT.setSymbol(exI.getSymbol());
			:} 
		| additive_expression:exI NE additive_expression:exD
			{: 
				printModoDepuracion("expression -> NE");
				RESULT = new NodoOperacion(exI,tipoOp.diferente,exD);
				RESULT.setSymbol(exI.getSymbol());
			:}
		| additive_expression:ex
			{: 
				printModoDepuracion("expression -> additive_expression");
				RESULT = ex;
				RESULT.setSymbol(ex.getSymbol());
			:}
		;

additive_expression  ::=
		additive_expression:exI PLUS multiplicative_expression:exD
			{: 
				printModoDepuracion("additive_expression -> PLUS");
				RESULT = new NodoOperacion(exI,tipoOp.mas,exD);
				RESULT.setSymbol(exD.getSymbol());
			:}
		| additive_expression:exI MINUS multiplicative_expression:exD
			{: 
				printModoDepuracion("additive_expression -> MINUS");
				RESULT = new NodoOperacion(exI,tipoOp.menos,exD);
				RESULT.setSymbol(exD.getSymbol());
			:}
		| multiplicative_expression:ex
			{:
				printModoDepuracion("additive_expression -> multiplicative_expression");
				RESULT=ex;
				RESULT.setSymbol(ex.getSymbol());
			:}
		;

multiplicative_expression        ::= 
		multiplicative_expression:exI TIMES factor:exD
			{: 
				printModoDepuracion("multiplicative_expression -> TIMES");
				RESULT = new NodoOperacion(exI,tipoOp.por,exD);
				RESULT.setSymbol(exD.getSymbol());
			:}
		| multiplicative_expression:exI OVER factor:exD
			{: 
				printModoDepuracion("multiplicative_expression -> OVER");
				RESULT = new NodoOperacion(exI,tipoOp.entre,exD);
				RESULT.setSymbol(exD.getSymbol());
			:}
		| factor:ex
			{: 
				printModoDepuracion("multiplicative_expression -> factor");
				RESULT=ex;
				RESULT.setSymbol(ex.getSymbol());
			:}
		;

factor      ::= 
		LPAREN logical_expression:ex RPAREN	
			{: 
				printModoDepuracion("factor -> LPAREN logical_expression RPAREN");
				RESULT = ex;
				RESULT.setSymbol(ex.getSymbol());
			:}
		| NUM:valor
			{: 
				printModoDepuracion("factor -> NUM");
				if(valor!=null){
					RESULT= new NodoValor(valor);	
					RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());			
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla factor -> NUM");
					System.exit(1);
				}
			:}
		| MINUS NUM:valor 
        	{: 
				printModoDepuracion("factor -> MINUS NUM");
				if(valor!=null){
					RESULT= new NodoValor(valor*-1);	
					RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());						
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla factor -> NUM NEGATIVO");
					System.exit(1);
				}
			:}
		| init_id:variable
			{: 
				printModoDepuracion("factor -> ID");
				if(variable!=null){
					//System.out.println("Construyendo Nodo ID para Lexema: "+ variable.toString());
					RESULT= variable;	
					RESULT.setSymbol(variable.getSymbol());	
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla factor -> ID");
					System.exit(1);
				}
			:}
		| TRUE   
			{: 
				printModoDepuracion("factor -> TRUE");
				RESULT= new NodoValor(true);		
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());				
			:}
		| FALSE
			{: 
				printModoDepuracion("factor -> FALSE");
				RESULT= new NodoValor(false);	
				RESULT.setSymbol((ComplexSymbol)CUP$parser$stack.peek());					
			:}
		| array_var_access:vesc	
			{: 
				printModoDepuracion("expression -> array_var_access");
				RESULT=vesc;
				RESULT.setSymbol(vesc.getSymbol());	
			:}
		| function_sentence:vesc	
			{: 
				printModoDepuracion("expression -> function_sentence");
				RESULT=vesc;
				RESULT.setSymbol(vesc.getSymbol());	
			:}
		| error
			{: 
				System.out.println("Ocurrio error en cup regla factor");
				RESULT=null;
				System.exit(1);
			:}
		;

array_var_access	::= 
		init_id:variable LCLASP additive_expression:pos RCLASP
			{:
				printModoDepuracion("array_var_access");
				if(variable!=null && pos!=null){
					RESULT= new NodoArray(variable, pos);	
					RESULT.setSymbol(variable.getSymbol());				
				}else{
					RESULT = null;
					System.out.println("Ocurrio error en cup regla array_var_access");
					System.exit(1);
				}
			:}	
		;


arguments_function ::= 
		logical_expression:le COMA arguments_function:af
			{:
				printModoDepuracion("arguments_function -> logical_expression COMA arguments_function");
				RESULT = new NodoParamFunction(le,af);
			:}
		| logical_expression:le
			{:
				printModoDepuracion("arguments_function -> logical_expression");
				RESULT = new NodoParamFunction(le);
			:}
		;
